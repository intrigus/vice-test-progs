; When the zero page in non-shared memory is relocated to page $e0 in
; bank 1 with page $e0 in shared memory space, and you read the zp in
; bank 0, do you read page $e0 in bank 1 ?
;
; test confirmed on real hardware
;
; Test made by Marco van den Heuvel


start=$2400

basicHeader=1 

!ifdef basicHeader {
; 10 SYS7181
*=$1c01  
	!byte  $0c,$08,$0a,$00,$9e,$37,$31,$38,$31,$00,$00,$00
*=$1c0d 
	jmp start
}
*=start

	sei
	lda #$00
	sta $ff00 ; map in I/O, bank 0
	lda #$07
	sta $d506 ; bottom shared memory 16KB
	lda #$aa
	sta $80   ; store #$aa in zero page in bank 0
	lda #$3f
	sta $ff00 ; bank 0 all ram
	lda #$55
	sta $e080 ; store #$55 in $e080 in bank 0
	lda #$7f
	sta $ff00 ; bank 1 all ram
	lda #$33
	sta $e080 ; store #$33 in $e080 in bank 1
	lda #$00
	sta $ff00 ; map in I/O, bank 0
	lda #$0a
	sta $d506 ; top shared memory 8KB
	lda #$01
	sta $d508
	lda #$e0
	sta $d507 ; relocate zero page to $e0xx in bank 1
	lda $80
	ldx #$00
	stx $d508
	ldx #$00
	stx $d507 ; put zero page back to $00xx in bank 0
	ldx #0
	cmp #$aa
	beq failed
	ldx #1
	cmp #$55
	beq failed
	cmp #$33
	beq passed
	ldx #4
	beq failed

passed:
	lda #$04
	sta $d506
	ldx #0	
-	
	lda ok_msg,x
	beq +
	sta $402,x
	inx
	jmp -
+
	lda #5
	sta $d020
	lda #$00
	sta $d7ff
	jmp *

failed:
	lda #$04
	sta $d506
	ldy #0	
-	
	lda error_msg,y
	beq +
	sta $402,y
	iny
	jmp -
+
	stx $d020
	lda #$ff
	sta $d7ff
	jmp *	

error_msg:
	!scr "test failed" 
	!byte 0
ok_msg:	
	!scr "test passed" 
	!byte 0
