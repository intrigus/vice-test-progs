 .word $0801

; *** Symbol table  follows ***

 zINDEX        = $22
 zFREKZP       = $FB
 vControl      = $D011
 vSprEnable    = $D015
 sVoc3FreqLo   = $D40E
 sVoc3FreqHi   = $D40F
 sVoc3Control  = $D412
 sVoc3Osc      = $D41B
 kCHROUT       = $FFD2

; *** text follows ***

    * = $0801
    .byte $0b,$08,$01,$00,$9e,$32,$30,$36,$31,$00,$00,$00
    jmp start
        
    .dsb $1000 - *, 0

    *=$1000
start

    JSR disable
    LDX #<txtsid
    LDY #>txtsid
    JSR print
    JSR sidver
    sta $0400
; 1 = 6581, 0 = 8580
    BEQ mos8580
mos6581    
    LDX #<txt6581
    LDY #>txt6581
    JMP output
mos8580    
    LDX #<txt8580
    LDY #>txt8580
output    
    JSR print
    JSR enable
    RTS

disable    
    SEI    
    LDA #$00
    STA vSprEnable
    LDA vControl
    AND #$EF
    STA vControl
vblank    ; Wait for vblank (raster = 0)
raslo    
    BIT vControl
    BPL raslo
rashi    
    BIT vControl
    BMI rashi
    RTS    

enable    
    LDA vControl
    ORA #$10
    STA vControl
    CLI
    RTS

print    
    STX zINDEX
    STY zINDEX+1
    LDY #$00
nxtchr    
    LDA (zINDEX),Y
    BEQ done
    JSR kCHROUT
    INY
    JMP nxtchr
done    
    RTS

sidver    
    LDA #$08
    STA sVoc3Control
    LDA #$00
    STA sVoc3FreqLo
    LDA #$40
    STA sVoc3FreqHi
    LDA #$20
    STA sVoc3Control
    LDA sVoc3Osc
    ; 1 = 6581, 0 = 8580
    ; The MOS8580 waveform is delayed one cycle.
    RTS

txtsid    
    .byte 13
    .asc "SID CHIP MODEL: "
    .byte 0
txt6581    
    .asc "MOS6581"
    .byte 13, 0
txt8580    
    .asc "MOS8580"
    .byte 13, 0
