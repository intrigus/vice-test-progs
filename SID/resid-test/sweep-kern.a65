    .word $0801

; *** Symbol table  follows ***

 zINDEX        = $22
 zFREKZP       = $FB
 vControl      = $D011
 vBorderCol    = $D020
 vSprEnable    = $D015
 sVoc3FreqLo   = $D40E
 sVoc3FreqHi   = $D40F
 sVoc3PwLo     = $D410
 sVoc3PwHi     = $D411
 sVoc3Control  = $D412
 sVoc3AttDec   = $D413
 sVoc3SusRel   = $D414
 sFcLo         = $D415
 sFcHi         = $D416
 sResFilt      = $D417
 sVolume       = $D418
 sVoc3Env      = $D41C
 kCHROUT       = $FFD2
 kGETIN        = $FFE4
 kSCNKEY       = $FF9F
 kSETLFS       = $FFBA
 kSETNAM       = $FFBD
 kSAVE         = $FFD8

; *** text follows ***

    * = $0801
    .byte $0b,$08,$01,$00,$9e,$32,$30,$36,$31,$00,$00,$00
    jmp start
        
    .dsb $1000 - *, 0

    * = $1000
start

    sei
    lda #$36
    sta $01
    lda #$00
    sta vSprEnable
    lda vControl
    and #$ef
    sta vControl
redo    
    lda #$0e
    sta vBorderCol
    lda #$00
    sta sVoc3FreqLo
    lda #$00
    sta sVoc3FreqHi
    lda #$00
    sta sVoc3PwLo
    lda #$08
    sta sVoc3PwHi
    lda #$00
    sta sVoc3AttDec
    ; Set test bit to reset oscillator, clear gate bit in case it was set
    lda #$08
    sta sVoc3Control
    ; Filter cutoff frequency
    lda #$00
    sta sFcLo
    lda #$40
    stx sFcHi
    ; Bandpass output
    lda #$2f
    sta sVolume

    ; Wait for key 1 - 4
getnum    
    jsr kSCNKEY
    jsr kGETIN
    sec
    sbc #$31
    bcc getnum
    cmp #4
    bcs getnum
    tax
    lda susrel,x
    ldy resfilt,x
    tax

    ; Set sustain level
    stx sVoc3SusRel
    ; Set resonance and filter input
    sty sResFilt

    ; Keep test bit on, set gate bit to start envelope
    lda #$09
    sta sVoc3Control

    ; Wait a complete envelope to ensure that sustain state is reached
    ; A complete envelope is 32352 cycles, we wait ca. 33 * 1000 cycles.
    ldy #33
delayy    
    ldx #199
delayx    
    dex
    bne delayx
    dey
    bne delayy

    lda #$00
    sta vBorderCol

getcr    
    jsr kSCNKEY
    jsr kGETIN
    cmp #$0d
    bne getcr

    ; Start triangle waveform
    lda #$11
    sta sVoc3Control
    lda #$00
    sta zFREKZP
    lda #$00
    sta zFREKZP+1
sweep    
    lda zFREKZP
    clc
    adc #$01
    sta zFREKZP
    lda zFREKZP+1
    adc #$00
    bcs done
    sta zFREKZP+1
    lda zFREKZP
    sta sVoc3FreqLo
    lda zFREKZP+1
    sta sVoc3FreqHi
    clc
    bcc sweep
done    jmp redo

    lda #$00
    sta sVoc3Control
    lda #$00
    sta sVolume
    lda #$0e
    sta vBorderCol
    lda vControl
    ora #$10
    sta vControl
    lda #$37
    sta $01
    cli
    rts

susrel    
    .byte $40, $40, $f0, $f0
resfilt    
    .byte $04, $f4, $04, $f4
